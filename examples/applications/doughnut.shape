/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

##needs revolutionbody
##needs circle

doughnutProfile:
[rotate3D dir:(1,0,0) angle:~90°]
[]
immerse
[]
[shift (3cm,0cm)]
[]
[circle 1.5cm]

square: \ x .> x * x

doubleDoughnutProfile:
[rotate3D dir:(1,0,0) angle:~90°]
[]
immerse
[]
[shift (3cm,0cm)]
[]
( @defaultunit:1%C
  |
  [[range 0° 359° 10°].foldl
  	\ p e .>
  	{
  	  p--(^)<((0.5cm+1cm*[square [abs [cos e]]])*[dir e])>(^)
  	}
  	emptypath]--cycle
)

doughnut:
    @nonstroking:[gray 0.8]
  & @width:0.01bp            |** used to fill in object edges
  & @stroking:[gray 0.8]    |** used to fill in object edges
	& @join:JOIN_ROUND
  & @reflections:0.7*[phong 25] + 0.3*[phong 0.5]
  & @autointensity:[gray 0.3]
  & @autoscattering:[phong 0.2]
  & @facetresolution:1cm
  & @shadeorder:'0          |** use '2 when targeting Adobe Reader or other viewers that can deal with gradient fills.
	|
|**  [revolutionBody doughnutProfile phiSteps:'25 thetaSteps:'37]
  [revolutionBody doubleDoughnutProfile phiSteps:'36 thetaSteps:'37]

lights: ( newLights << [shift (6cm,8cm,10cm)] [] [specular_light [gray 0.9]]
|**                    << [ambient_light [gray 0.3]]
										)
•world: newZSorter
•world << lights
			 << [rotate3D dir:(1,0,0) angle:~70°] [] doughnut
world: •world;

•page << @nonstroking:GRAY_BLACK | [fill [rectangle (~5cm,~4cm) (5cm,4cm)]]
•page << view [] world
