|** This example is included to compare Drool with Sketch.
|** Kjell Magne Fauske draw a nice robot arm using Sketch, and I want to see
|** what the corresponding Drool source could look like (if possible to reproduce at all).
|** Many thanks to Kjell Magne for letting me use his example.


#unit u = 1cm


cylinderWall: \ radius height sides .>
{
  deltaAngle: 360° / sides
  r: radius * ( 2 / ( 1 + [cos deltaAngle / 2] ) )
  oneSide: [facet (r,0u,0u)--(r*[cos deltaAngle],r*[sin deltaAngle],0u)--(r*[cos deltaAngle],r*[sin deltaAngle],height)--(r,0u,height)--cycle
                 [facetnormal (r,0u,0u) (1,0,0)] [facetnormal (r*[cos deltaAngle],r*[sin deltaAngle],0u) ([cos deltaAngle],[sin deltaAngle],0)] ]

  res: Hot3D <<

  ![[range '0 sides-'1].foldl
   \ p e .>
    !{
      res << [rotate3D (0,0,1) e*deltaAngle] [] oneSide
      p
    }
   VOID]

  res;
  res
}

polyDisc: \ radius sides .>
 [[range '0 sides-'1].foldl
  \ pile e .>  pile--( radius * [dir (360°/sides) * e] )
  EMPTYPATH2D]--cycle

cylinderLid: \ radius height sides smallRadius .>
{
  [shift (0cm,0cm,height)] [] ( Hot3D << [facet [immerse [polyDisc radius sides]]]
                                      << @nonstroking:[gray 0.3] | [facet [immerse [polyDisc smallRadius sides]] tiebreaker:1bp]
                                )
}

|** First object to be defined is a joint.

jointRadius: 1u
jointThickness: 0.5u  |** Take this times two to obtain the total height.
jointSurface:
    @nonstroking:[gray 0.8]
  & @reflections:0.3*[phong 25] + 0.7*[phong 0.5]
  & @facetresolution:0.5u


zbuf: HotZSorter <<

jointSides: '15

joint: jointSurface | ( Hot3D << [cylinderWall jointRadius 2*jointThickness jointSides]
                              << [cylinderLid jointRadius 0bp jointSides 0.3*jointRadius]
                              << [cylinderLid jointRadius 2*jointThickness jointSides 0.3*jointRadius]
			      << @nonstroking:[gray 1] | [stroke (0cm,0cm,~2.5*jointThickness)--(0cm,0cm,4.5*jointThickness)]
                        )

zbuf << [rotate3D (1,0,0) ~50°] [] joint

zbuf << [shift (0cm,0cm,10cm)] [] [specular_light [gray 0.9]]
zbuf << [ambient_light [gray 0.3]]
zbuf;

@<< @nonstroking:RGB_GREEN | [fill [rectangle (~3u,~3u) (3u,3u)]]
@<< @eyez:25u|[view zbuf]
