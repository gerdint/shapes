/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

rot: [rotate3D (1,1,1) 30°]

screwSeg: (~1cm,1cm,~1cm)<(~1cm,0cm,~1cm)>(~1cm,~1cm,~1cm)--(1cm,~1cm,~2cm)<(1cm,0cm,~2cm)>(1cm,1cm,~2cm)
deeper: \ n → [shift (0cm,0cm,n*~2cm)]
screw: [scale3D 1.5] [] (1cm,~1cm,0cm)<(1cm,0cm,0cm)>(1cm,1cm,0cm)--screwSeg--[[deeper 1] screwSeg]--[[deeper 2] screwSeg]--cycle

•world: newGroup3D

•world << @width:0.3bp & @stroking:[gray 0.5] | [stroke screw]

markStart: 0.15
markCount: '5

paintLeg: \ pth → ( [stroke pth] & ( @nonstroking:RGB_BLACK | [fill [shift pth.end.p][][immerse [circle 1.5bp]]] ) )

•world <<
  [[range '0 markCount-'1].foldl
    \ p i →
    {
      sl: [screw (markStart + i*(1/markCount))*[abs screw]]
      len: 2.5cm
      p &
      ( newGroup3D
        << @stroking:RGB_RED   | [paintLeg sl.p--(+(len*sl.T))]
        << @stroking:RGB_GREEN | [paintLeg sl.p--(+(len*sl.N))]
        << @stroking:RGB_BLUE  | [paintLeg sl.p--(+([if i = '0 ~1 1]*len*sl.B))]
        << [shift sl.p+(~2mm,3mm,0mm)] [] [facing [center_x (TeX [sprintf `\textbf{%d}´ i+'1])]] )
    }
    null3D]

•world << [shift (~1.5cm,~1.5cm,0cm)] [] [facing [center_x (TeX `Odd one out!´)]]

world: •world;

{
  world: [rotate3D (1,1,1) 30°] [] ../world
  eyew: 5.0cm
  •page << view [] world
  •page << world >> [shift (~eyew,0cm,0cm)] >> view >> [shift (2*eyew,0cm)]
}
