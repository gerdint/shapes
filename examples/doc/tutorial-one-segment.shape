##needs circle

pathStyle: @width: 1bp
controlStyle: @width: 0.3bp & @dash:[dashpattern 2mm 2mm] & @stroking:[gray 0.5]
onPathMark: @nonstroking:RGB_RED | [fill [circle 1mm]]
controlHead: \ p .> (> cut:0bp
                       picture: @stroking:RGB_RED & @dash:[dashpattern] | ([shift p.begin.p]*[rotate [angle p.begin.T]])[][stroke (~1.5mm,~1.5mm)--(1.5mm,1.5mm) & (~1.5mm,1.5mm)--(1.5mm,~1.5mm)]
                    <)

twoHandlesHelper: \ p0 p1 p2 p3 .>
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( controlStyle | [stroke p0--p1 head:controlHead] )
	&
	( controlStyle | [stroke p3--p2 head:controlHead] )
	&
	( pathStyle | [stroke p0>p1--p2<p3] )
)

frontHandlesHelper: \ p0 p1 p3 .>
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( controlStyle | [stroke p0--p1 head:controlHead] )
	&
	( pathStyle | [stroke p0>p1--p3] )
)

rearHandlesHelper: \ p0 p2 p3 .>
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( controlStyle | [stroke p3--p2 head:controlHead] )
	&
	( pathStyle | [stroke p0--p2<p3] )
)

noHandlesHelper: \ p0 p3 .>
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( pathStyle | [stroke p0--p3] )
)

twoHandles: \ s1 s2 .>
{
	p0: (0cm,0cm)
	p3: (6cm,1cm)
	p1: p0+s1*1.5cm*[dir 60°]
	p2: p3+s2*3cm*[dir ~165°]
	[if s1 == 0
			[if s2 == 0
				  [noHandlesHelper p0 p3]
				  [rearHandlesHelper p0 p2 p3]]
			[if s2 == 0
				  [frontHandlesHelper p0 p1 p3]
				  [twoHandlesHelper p0 p1 p2 p3]]]
}

##unit dy = 1cm
##unit xStep = 7cm

•page << [shift (0xStep,0dy)] [] [twoHandles 1 1]
			<< [shift (0xStep,~1dy)] [] [twoHandles 1 1/2]
			<< [shift (0xStep,~2dy)] [] [twoHandles 1 1/4]
			<< [shift (0xStep,~3dy)] [] [twoHandles 1 0.001]
			<< [shift (0xStep,~4dy)] [] [twoHandles 1 0]
			<< [shift (1xStep,~4dy)] [] [twoHandles 1/2 0]
			<< [shift (2xStep,~4dy)] [] [twoHandles 1/4 0]
			<< [shift (3xStep,~4dy)] [] [twoHandles 0.001 0]
			<< [shift (4xStep,~4dy)] [] [twoHandles 0 0]
