/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

pathStyle: @width: 1bp
controlStyle: @width: 0.3bp & @dash:[dashpattern 2mm 2mm] & @stroking:[gray 0.5]
onPathMark: @nonstroking:RGB_RED | [fill [circle 1mm]]
controlHead: \ p → (> cut:0bp
                       picture: @width:1bp & @stroking:RGB_RED & @dash:[dashpattern] | ([shift p.begin.p]*[rotate [angle p.begin.T]])[][stroke (~1.5mm,~1.5mm)--(1.5mm,1.5mm) & (~1.5mm,1.5mm)--(1.5mm,~1.5mm)]
                    <)

twoHandlesHelper: \ p0 p1 p2 p3 →
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( controlStyle | [stroke p0--p1 head:controlHead] )
	&
	( controlStyle | [stroke p3--p2 head:controlHead] )
	&
	( pathStyle | [stroke p0>p1--p2<p3] )
)

frontHandlesHelper: \ p0 p1 p3 →
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( controlStyle | [stroke p0--p1 head:controlHead] )
	&
	( pathStyle | [stroke p0>p1--p3] )
)

rearHandlesHelper: \ p0 p2 p3 →
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( controlStyle | [stroke p3--p2 head:controlHead] )
	&
	( pathStyle | [stroke p0--p2<p3] )
)

noHandlesHelper: \ p0 p3 →
(
	[[shift p0] onPathMark]
	&
	[[shift p3] onPathMark]
	&
	( pathStyle | [stroke p0--p3] )
)

twoHandles: \ s1 s2 →
{
	p0: (0cm,0cm)
	p3: (6cm,1cm)
	p1: p0+s1*1.5cm*[dir 60°]
	p2: p3+s2*3cm*[dir ~165°]
	[if s1 = 0
			[if s2 = 0
				  [noHandlesHelper p0 p3]
				  [rearHandlesHelper p0 p2 p3]]
			[if s2 = 0
				  [frontHandlesHelper p0 p1 p3]
				  [twoHandlesHelper p0 p1 p2 p3]]]
}

##unit dy = 1cm
##unit dx = 7cm

•page << [shift (0dx,0dy)] [] [twoHandles 1 1]
			<< [shift (0dx,~1dy)] [] [twoHandles 1 1/2]
			<< [shift (0dx,~2dy)] [] [twoHandles 1 1/4]
			<< [shift (0dx,~3dy)] [] [twoHandles 1 0.001]
			<< [shift (0dx,~4dy)] [] [twoHandles 1 0]

			<< [shift (1dx,~4dy)] [] [twoHandles 1/2 0]
			<< [shift (2dx,~4dy)] [] [twoHandles 1/4 0]
			<< [shift (3dx,~4dy)] [] [twoHandles 0.001 0]
			<< [shift (4dx,~4dy)] [] [twoHandles 0 0]

			<< [shift (1dx,0dy)] [] [twoHandles 1/2 1]
			<< [shift (2dx,0dy)] [] [twoHandles 1/4 1]
			<< [shift (3dx,0dy)] [] [twoHandles 0.001 1]
			<< [shift (4dx,0dy)] [] [twoHandles 0 1]

			<< [shift (4dx,~1dy)] [] [twoHandles 0 1/2]
			<< [shift (4dx,~2dy)] [] [twoHandles 0 1/4]
			<< [shift (4dx,~3dy)] [] [twoHandles 0 0.001]
