|** This is "blockdraw, Waiting For Object oriented programming

##needs shiftoff
##needs centering
##needs circle
##needs arrowheads

dynamic @longblockrx identity 0.7cm
dynamic @longblockry identity 0.4cm
dynamic @smallblockr identity 0.3cm
dynamic @fracblockry identity 0.5cm

dynamic @connectionlw identity 0.8bp
dynamic @blocklw identity 0.8bp

dynamic @abovelabelmargin identity 1.5mm
dynamic @belowlabelmargin identity 1.5mm
dynamic @leftrightlabelmargin identity 1mm
dynamic @signmarginslide identity 1.5mm
dynamic @signmarginoffset identity 2mm

dynamic @blockspaceverynear identity 3mm
dynamic @blockspacenear identity 6mm
dynamic @blockspacelabelspace identity 8mm
dynamic @blockspacefar identity 10mm
dynamic @blockspaceveryfar identity 15mm

dynamic @textscaling identity [scale 1]

dynamic @connectpainter identity [stroke head:MetaPostArrow ...]

centerlabel: \ obj .> [centerx [centeryX obj]]

putlabelAbove: \ lbl z x .> [[shift z + (0cm,@abovelabelmargin)] [centerxat lbl x]]
putlabelBelow: \ lbl z x .> [[shift z + (0cm,~@belowlabelmargin)] [centeryatX [centerxat lbl x] 1]]
putlabelLeft: \ lbl z y .> [[shift z + (~@leftrightlabelmargin,0cm)] [centeryatX [centerxat lbl 1] y]]
putlabelRight: \ lbl z y .> [[shift z + (@leftrightlabelmargin,0cm)] [centeryatX lbl y]]
leftpointsign: \ obj str n:1 i:1 .> [[shift [leftpoint obj n i] + (~@signmarginslide,~@signmarginoffset)] [centerat (TeX [sprintf `$\scriptsize %s$´ str]) (0,0)]]
rightpointsign: \ obj str n:1 i:1 .> [[shift [rightpoint obj n i] + (@signmarginslide,@signmarginoffset)] [centerat (TeX [sprintf `$\scriptsize %s$´ str]) (0,0)]]
bottompointsign: \ obj str n:1 i:1 .> [[shift [bottompoint obj n i] + (@signmarginoffset,~@signmarginslide)] [centerat (TeX [sprintf `$\scriptsize %s$´ str]) (0,0)]]
toppointsign: \ obj str n:1 i:1 .> [[shift [toppoint obj n i] + (~@signmarginoffset,@signmarginslide)] [centerat (TeX [sprintf `$\scriptsize %s$´ str]) (0,0)]]

putblockOrigin: \ •dst newBlock .> {
  •dst << newBlock
  newBlock
}

putblockLeft: \ •dst newBlock oldBlock dist .> {
  res: [[shift [leftpoint oldBlock 1 1]+(~dist,0)] [centerxat newBlock 1]]
  •dst << res
  res
}
putblockNearLeft: \ •dst newBlock oldBlock .> [putblockLeft •dst newBlock oldBlock @blockspacenear]
putblockFarLeft: \ •dst newBlock oldBlock .> [putblockLeft •dst newBlock oldBlock @blockspacefar]
putblockVeryNearLeft: \ •dst newBlock oldBlock .> [putblockLeft •dst newBlock oldBlock @blockspaceverynear]
putblockVeryFarLeft: \ •dst newBlock oldBlock .> [putblockLeft •dst newBlock oldBlock @blockspaceveryfar]
putblockLabelSpaceLeft: \ •dst newBlock oldBlock .> [putblockLeft •dst newBlock oldBlock @blockspacelabelspace]
putblockLabelMarginLeft: \ •dst newBlock oldBlock .> [putblockLeft •dst newBlock oldBlock @leftrightlabelmargin]

putblockRight: \ •dst newBlock oldBlock dist .> {
  res: [[shift [rightpoint oldBlock 1 1]+(dist,0)] [centerxat newBlock ~1]]
  •dst << res
  res
}
putblockNearRight: \ •dst newBlock oldBlock .> [putblockRight •dst newBlock oldBlock @blockspacenear]
putblockFarRight: \ •dst newBlock oldBlock .> [putblockRight •dst newBlock oldBlock @blockspacefar]
putblockVeryNearRight: \ •dst newBlock oldBlock .> [putblockRight •dst newBlock oldBlock @blockspaceverynear]
putblockVeryFarRight: \ •dst newBlock oldBlock .> [putblockRight •dst newBlock oldBlock @blockspaceveryfar]
putblockLabelSpaceRight: \ •dst newBlock oldBlock .> [putblockRight •dst newBlock oldBlock @blockspacelabelspace]
putblockLabelMarginRight: \ •dst newBlock oldBlock .> [putblockRight •dst newBlock oldBlock @leftrightlabelmargin]

putblockBelow: \ •dst newBlock oldBlock dist .> {
  res: [[shift [bottompoint oldBlock 1 1]+(0,~dist)] [centeryat newBlock 1]]
  •dst << res
  res
}
putblockNearBelow: \ •dst newBlock oldBlock .> [putblockBelow •dst newBlock oldBlock @blockspacenear]
putblockFarBelow: \ •dst newBlock oldBlock .> [putblockBelow •dst newBlock oldBlock @blockspacefar]
putblockVeryNearBelow: \ •dst newBlock oldBlock .> [putblockBelow •dst newBlock oldBlock @blockspaceverynear]
putblockVeryFarBelow: \ •dst newBlock oldBlock .> [putblockBelow •dst newBlock oldBlock @blockspaceveryfar]
putblockLabelSpaceBelow: \ •dst newBlock oldBlock .> [putblockBelow •dst newBlock oldBlock @blockspacelabelspace]
putblockLabelMarginBelow: \ •dst newBlock oldBlock .> [putblockBelow •dst newBlock oldBlock @belowlabelmargin]

putblockAbove: \ •dst newBlock oldBlock dist .> {
  res: [[shift [toppoint oldBlock 1 1]+(0,dist)] [centeryat newBlock ~1]]
  •dst << res
  res
}
putblockNearAbove: \ •dst newBlock oldBlock .> [putblockAbove •dst newBlock oldBlock @blockspacenear]
putblockFarAbove: \ •dst newBlock oldBlock .> [putblockAbove •dst newBlock oldBlock @blockspacefar]
putblockVeryNearAbove: \ •dst newBlock oldBlock .> [putblockAbove •dst newBlock oldBlock @blockspaceverynear]
putblockVeryFarAbove: \ •dst newBlock oldBlock .> [putblockAbove •dst newBlock oldBlock @blockspaceveryfar]
putblockLabelSpaceAbove: \ •dst newBlock oldBlock .> [putblockAbove •dst newBlock oldBlock @blockspacelabelspace]
putblockLabelMarginAbove: \ •dst newBlock oldBlock .> [putblockAbove •dst newBlock oldBlock @abovelabelmargin]

sizedblock: \ txt rx ry .>
 (
   [centerlabel [@textscaling txt]]
   &
   ( @width:@blocklw | [stroke (~rx,~ry)--(~rx,ry)--(rx,ry)--(rx,~ry)--cycle] )
 )

longblock: \ txt .> [sizedblock txt @longblockrx @longblockry]
squareblock: \ txt .> [sizedblock txt @smallblockr @smallblockr]
fracblock: \ txt .> [sizedblock txt @longblockrx @fracblockry]

longenoughblock: \ txt .> [sizedblock txt [max @longblockrx 1mm+0.5*([xmax [bbox txt]]-[xmin [bbox txt]])] @longblockry]
longenoughfracblock: \ txt .> [sizedblock txt [max @longblockrx 1mm+0.5*([xmax [bbox txt]]-[xmin [bbox txt]])] @fracblockry]

roundblock: \ txt .>
  (
    [centerat [@textscaling txt] (0,0)]
    &
    ( @width:@blocklw | [stroke [circle @smallblockr]] )
  )

sumpicture: \ .> [roundblock (TeX `$\Sigma$´)]

splitdot: \ .> ( @nonstroking:@stroking | [fill [circle 2.5 * @connectionlw]] )

termcircle: \ .> ( @width:@connectionlw | [stroke [circle 3 * @connectionlw]] )

hhconnect: \ •dst pa pb mediation slide .>
 {
   mid: [mediate mediation pa.x pb.x] + [if pb.x > pa.x 1 ~1] * slide
   respath: pa--( mid, pa.y )--( mid, pb.y )--pb
   •dst << @width:@connectionlw | [@connectpainter respath]
   respath
  }
vvconnect: \ •dst pa pb mediation slide .>
  {
    mid: [mediate mediation pa.y pb.y] + [if pb.y > pa.y 1 ~1 ] * slide
    respath: pa--( pa.x, mid )--( pb.x, mid )--pb
    •dst << @width:@connectionlw | [@connectpainter respath]
    respath
  }
hvconnect: \ •dst pa pb .>
  {
    respath: pa--( pb.x, pa.y )--pb
    •dst << @width:@connectionlw | [@connectpainter respath]
    respath
  }
vhconnect: \ •dst pa pb .>
  {
    respath: pa--( pa.x, pb.y )--pb
    •dst << @width:@connectionlw | [@connectpainter respath]
    respath
  }

conlabel: \ shiftdir txt z .> [[shift z] [shiftoffwlm txt shiftdir]]

leftpoint:   \ pic n:1 i:1 .> { bb: [bbox pic] ([xmin bb], [mediate i/(n+1) [ymin bb] [ymax bb]])}
rightpoint:  \ pic n:1 i:1 .> { bb: [bbox pic] ([xmax bb], [mediate i/(n+1) [ymin bb] [ymax bb]])}
bottompoint: \ pic n:1 i:1 .> { bb: [bbox pic] ([mediate i/(n+1) [xmin bb] [xmax bb]], [ymin bb])}
toppoint:    \ pic n:1 i:1 .> { bb: [bbox pic] ([mediate i/(n+1) [xmin bb] [xmax bb]], [ymax bb])}

llconnect: \ •dst pica picb slide .> { z1:[leftpoint pica 1 1]   z2:[leftpoint picb 1 1]   [hhconnect •dst z1 z2 [if z1.x<z2.x 0 1] slide] }
rrconnect: \ •dst pica picb slide .> { z1:[rightpoint pica 1 1]  z2:[rightpoint picb 1 1]  [hhconnect •dst z1 z2 [if z1.x>z2.x 0 1] slide] }
bbconnect: \ •dst pica picb slide .> { z1:[bottompoint pica 1 1] z2:[bottompoint picb 1 1] [vvconnect •dst z1 z2 [if z1.x<z2.x 0 1] slide] }
ttconnect: \ •dst pica picb slide .> { z1:[toppoint pica 1 1]    z2:[toppoint picb 1 1]    [vvconnect •dst z1 z2 [if z1.x>z2.x 0 1] slide] }

lrconnect: \ •dst pica picb .> [hhconnect •dst [leftpoint pica 1 1] [rightpoint picb 1 1] 0.5 0cm]
rlconnect: \ •dst pica picb .> [hhconnect •dst [rightpoint pica 1 1] [leftpoint picb 1 1] 0.5 0cm]
btconnect: \ •dst pica picb .> [vvconnect •dst [bottompoint pica 1 1] [toppoint picb 1 1] 0.5 0cm]
tbconnect: \ •dst pica picb .> [vvconnect •dst [toppoint pica 1 1] [bottompoint picb 1 1] 0.5 0cm]

ltconnect: \ •dst pica picb .> [hvconnect •dst [leftpoint pica 1 1] [toppoint picb 1 1]]
lbconnect: \ •dst pica picb .> [hvconnect •dst [leftpoint pica 1 1] [bottompoint picb 1 1]]
rtconnect: \ •dst pica picb .> [hvconnect •dst [rightpoint pica 1 1] [toppoint picb 1 1]]
rbconnect: \ •dst pica picb .> [hvconnect •dst [rightpoint pica 1 1] [bottompoint picb 1 1]]
tlconnect: \ •dst pica picb .> [vhconnect •dst [toppoint pica 1 1] [leftpoint picb 1 1]]
trconnect: \ •dst pica picb .> [vhconnect •dst [toppoint pica 1 1] [rightpoint picb 1 1]]
blconnect: \ •dst pica picb .> [vhconnect •dst [bottompoint pica 1 1] [leftpoint picb 1 ]]
brconnect: \ •dst pica picb .> [vhconnect •dst [bottompoint pica 1 1] [rightpoint picb 1 1]]

connect: \ •dst pica picb .>
{
  bba: [bbox pica]
  bbb: [bbox picb]
  rxa: 0.5 * ( [xmax bba] - [xmin bba] )
  rxb: 0.5 * ( [xmax bbb] - [xmin bbb] )
  rya: 0.5 * ( [ymax bba] - [ymin bba] )
  ryb: 0.5 * ( [ymax bbb] - [ymin bbb] )
  ca: [discrete_mean bba]
  cb: [discrete_mean bbb]
  [if ca.x < cb.x - ( rxa + rxb )
      [if ca.y < cb.y - ( rya + ryb )
      	[rbconnect •dst pica picb]
      [if ca.y > cb.y + ( rya + ryb )
	[rtconnect •dst pica picb]
	[rlconnect •dst pica picb]
      ]]
  [if ca.x > cb.x + ( rxa + rxb )
      [if ca.y < cb.y - ( rya + ryb )
	[lbconnect •dst pica picb]
      [if ca.y > cb.y + ( rya + ryb )
	[ltconnect •dst pica picb]
	[lrconnect •dst pica picb]
      ]]
      [if ca.y < cb.y - ( rya + ryb )
	[tbconnect •dst pica picb]
      [if ca.y > cb.y + ( rya + ryb )
	[btconnect •dst pica picb]
	[hvconnect •dst ca cb]
      ]]
    ]]
}
