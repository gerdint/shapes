/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

##needs centering

size1: @text_size:10bp & @text_horizontalscaling:1 & @text_characterspacing:0.45bp
size2: @text_size:16bp
size3: @text_size:26bp & @text_horizontalscaling:1
font: @text_font:FONT_HELVETICA & @text_leading:1.3
sentenceSep: 0.8

leftMargin: 20mm
topbotMargin: 20mm

pageFrame_S5: [rectangle (0mm,0mm) (165mm,240mm)]
pageFrame_A4: [rectangle (0mm,0mm) (210mm,297mm)]


•helpers: newGroup
helperStyle: @width:0bp & @dash:[dashpattern 3bp 2mm] & @stroking:[rgb 33.3*0.01	44.7*0.01	57.3*0.01]

y1: 240mm - topbotMargin
line1: [shift (leftMargin,y1)] [] [centerat font & size1 | ( newText << [kerning `Linköping studies in science and technology.´ ~sentenceSep `Dissertations.´ ~sentenceSep `No. 1166´] ) (~1,1)]
•page << line1

y2: [ymin [bbox line1]] - 20mm
line2: [shift (leftMargin,y2)] [] [centerat size3 & font | @text_font:FONT_HELVETICA | ( newText <<`
Optimal Control and
Model Reduction of
Nonlinear DAE Models
´ ) (~1,1)]
•page << line2

y3: [ymin [bbox line2]] - 20mm
line3: [shift (leftMargin,y3)] [] [centerat font & size2 | ( newText << `Johan Sjöberg´ ) (~1,1)]
•page << line3

y4: [ymin [bbox line3]] - 15mm
sheet: [import `johans.pdf´] [] '0   |** import all pages, and select page 0
•page << [shift (leftMargin-5mm,y4)] [] [centerat sheet (~1,1)]

y5: topbotMargin
•helpers << helperStyle | [stroke (0mm,y5)--(170mm,(+0mm))]
•page << [shift (leftMargin,y5)] [] ( font & size1 | ( newText << `Linköping 2008´ ) )

LiTH: [import `LiTH_staende_eng_sv.pdf´] [] '0   |** import all pages, and select page 0
•page << [shift ([xmax pageFrame_S5]-leftMargin-4mm,y5-2.3mm)] [] [centerat [scale 30mm/([ymax [bbox LiTH]]-[ymin [bbox LiTH]])] [] LiTH (1,~1)]

•helpers << helperStyle | [stroke (5mm,0)--((+0mm),240mm)]
•helpers << [bboxed helperStyle | [stroke pageFrame_S5]
				     				[shift (~20mm,~30mm)] [] pageFrame_A4]

{
  tmp: [bboxed [clip (•page) pageFrame_S5] pageFrame_S5]
	[erase •page]
  •page << tmp
}

/** Comment out the line below to remove helper lines!
 **/
|** •page << (•helpers)
