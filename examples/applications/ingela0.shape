##needs arrowheads
##needs circle

ingelasArrow: \ p .> [metaPostArrow p ahLength:2mm]

xmax: \ p .> [continuous_maximizer p (1,0)].p.x
xmin: \ p .> [continuous_maximizer p (~1,0)].p.x
ymax: \ p .> [continuous_maximizer p (0,1)].p.y
ymin: \ p .> [continuous_maximizer p (0,~1)].p.y

mediate: \ r x1 x2 .> (1-r)*x1 + r*x2

leftpoint:   \ pic n i .> { bb: [bbox pic]   ([xmin bb], [mediate i/(n+1) [ymin bb] [ymax bb]]) }
rightpoint:  \ pic n i .> { bb: [bbox pic]   ([xmax bb], [mediate i/(n+1) [ymin bb] [ymax bb]]) }
bottompoint: \ pic n i .> { bb: [bbox pic]   ([mediate i/(n+1) [xmin bb] [xmax bb]], [ymin bb]) }
toppoint:    \ pic n i .> { bb: [bbox pic]   ([mediate i/(n+1) [xmin bb] [xmax bb]], [ymax bb]) }

centerat: \ obj z .>
{
  thebb: [bbox obj]
  [[shift (0.5*([xmin thebb]*(z.x-1) - [xmax thebb]*(z.x+1)),
    	   0.5*([ymin thebb]*(z.y-1) - [ymax thebb]*(z.y+1)) )]
   obj]
}
  	   

@width: 0.5bp
& @defaultunit:1%f
|
{

box: \ x0y0 x1y1 .> x0y0--(x0y0.x,x1y1.y)--x1y1--(x1y1.x,x0y0.y)--cycle

stdBox: \ .>
{
  tmp: [box (~5mm,~3mm) (5mm,3mm)]
  ( @nonstroking:gray_WHITE|[fill tmp] ) & [stroke tmp]
}

rPlus: 2mm
plusCircle: \ .>
{
  c: [circle rPlus]
  ( newGroup2D
    << @nonstroking:gray_WHITE|[fill c] << [stroke c]
    << [centerat [teX `$+$´] (0,0)] )
}

sigmoidPic:
@width:0.3bp &
@stroking:void &
@defaultunit:1%f
|
(
  newGroup2D << [stroke (~4.5mm,0)--(4.5mm,0)] << [stroke (0,~2.5mm)--(0,2.5mm)] << [stroke (~4.5mm,~2.5mm)>(6mm^0°)--(6mm^180°)<(4.5mm,2.5mm)]
)

templ: \ symbs:false .>
{
  theColor:[if symbs gray_BLACK [gray 0.7]]
  box1: @stroking:theColor|[stdBox]
  sum: [[shift (11.5mm,0)] @stroking:theColor|[plusCircle]]
  box2: [[shift (23mm,0)] box1]
  box3: [[shift (37mm,0)] box1]
  box4: [[shift (0,~1cm)] box1]
  startPoint: \ n pic .> [leftpoint pic 3 n]+(~1cm,(n-2)*3mm)
  lineLeft: \ n pic .> [startPoint n pic]>(^0°)--(^~180°)<[leftpoint pic 3 n]
  •res: newGroup2D
  @stroking:theColor &
  @nonstroking:theColor
  |
  { •res  << box1 << sum << box2 << box3 << box4
    	  << [stroke [rightpoint box1 1 1]--[leftpoint sum 1 1] head:ingelasArrow]
    	  << [stroke [rightpoint sum 1 1]--[leftpoint box2 1 1] head:ingelasArrow]
    	  << [stroke [rightpoint box2 1 1]--[leftpoint box3 1 1] head:ingelasArrow]
    	  << [stroke [rightpoint box4 1 1]>(^0°)--(^~90°)<[bottompoint sum 1 1] head:ingelasArrow]
    	  << [stroke [rightpoint box3 1 1]--[leftpoint [[shift (5cm,0)] [plusCircle]] 1 1] head:ingelasArrow]
	  << [stroke [lineLeft 1 box1] head:ingelasArrow]
	  << [stroke [lineLeft 2 box1] head:ingelasArrow]
	  << [stroke [lineLeft 3 box1] head:ingelasArrow]
	  << [[shift [startPoint 3 box1]] [centerat [teX `$u(t)$´] (1,0)]]
	  << [[shift [startPoint 2 box1]] [centerat [teX `$u(t-1)$´] (1,0)]]
	  << [[shift [startPoint 1 box1]] [centerat [teX `$u(t-2)$´] (1,0)]]
  }

   •res	  << [if symbs [centerat [teX `$\beta_{k}$´] (0,0)] null]
	  << [if symbs [[shift (0,~1cm)] [centerat [teX `$\gamma_{k}$´] (0,0)]] null]
	  << [if symbs [[shift (23mm,0)] sigmoidPic] null]
	  << [if symbs [[shift (37mm,0)] [centerat [teX `$\alpha_{k}$´] (0,0)]] null]
   •res;
}

dHat: [normalized (2cm,3cm)]
nDup: '30
dStep: 1mm
d: dStep*dHat*(1*(nDup-'1))
dpshift: \ len .> [shift len * dHat]

•page <<
  [[range nDup-'1 '1 '~1].foldl
       \ p e .> p & [[dpshift e*dStep] [templ]]
       null]

•page << [[dpshift 0mm] [templ true]]


plusBar:
@nonstroking:gray_WHITE
& @defaultunit:1%C
|
{
  f: rPlus*[normalized [[rotate 90°] d]]
  a: [angle d]
  cyl: (-f)--(d-f)>(^a)--(^)<(d+rPlus*[normalized d])>(^)--(^a)<(f+d)--f--cycle
  ( newGroup2D << [fill cyl] << [stroke cyl] << @nonstroking:gray_BLACK|[plusCircle] )
}

muBox: [[shift (37mm,~1cm)] [stdBox]]

•page
 << [[shift (5cm,0)] plusBar] << muBox << [[shift (37mm,~1cm)] [centerat [teX `$\mu$´] (0,0)]]
 << [stroke [rightpoint [[shift (37mm,~1cm)] [stdBox]] 1 1]>(^0°)--(^~90°)<[bottompoint [[shift (5cm,0)] [plusCircle]] 1 1] head:ingelasArrow]

py: (5cm+rPlus,0) + dHat*dStep*(nDup*(1/2))
py2: py + (1cm,0)
•page << [stroke py--py2 head:ingelasArrow] << [[shift py2+(1mm,0)] [centerat [teX `$y(t)$´] (~1,~1)]]

} /** end of "global" dynamic bindings **/
