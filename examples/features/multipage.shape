##needs circle

clock: \ hour minute .>
{
  hourLength: 4cm
  minuteLength: 5.5cm

  hourAngle: 90° - (hour/12)*360°
  minuteAngle: 90° - (minute/60)*360°

  [bboxed
    ( [stroke (0cm,0cm)--(hourLength*[dir hourAngle])]
      &
      [stroke (0cm,0cm)--(minuteLength*[dir minuteAngle])]
    )
    [rectangle (~minuteLength,~minuteLength) (minuteLength,minuteLength)]]
}

[setpagelabel •catalog prefix:`Am ´ number:'8]
•stdout << `First page's label: ´ << [nextpagelabel •catalog] << `¢n´

•catalog << [clock 8 0]
	    & [destination level:'0 text:`Morning´ color:[rgb 0.5 0.5 0] open:true]
	    & [destination level:'1 text:`Eight´]
	    & {
	      bar: [shift (3cm,1cm)][]stroke[][circle 5mm]
  	      bar
	      &
	      [annotationLink site:[site target:bar
	  			         text:`Leave this place!´
				         width: 2bp]
      	 		   name:`four in the afternoon´]
	    }
         << [clock 9 0]
	    & [destination level:'1 text:`Nine´]

[setpagelabel •catalog number:[nextpagenumber •catalog]+'1]
•stdout << `Skipping to page: ´ << [nextpagenumber •catalog] << `¢n´

•catalog << [clock [nextpagenumber •catalog]*1 0]

/** Note how the various part of the page label may be set independently of each other.
 **/
[setpagelabel •catalog prefix:`Pm ´]
[setpagelabel •catalog number:'33]
[setpagelabel •catalog style:'alphabet]
•stdout << `Later page's label: ´ << [nextpagelabel •catalog] << `¢n´

•catalog << [clock 3 10]
  	    & [destination level:'0 text:`Afternoon´ italic:true]
         << [clock 4 10]
  	    & [destination name:`four in the afternoon´]
	 << [clock 5 10]
	    & [destination level:'1 text:`Five´ bold:true]
