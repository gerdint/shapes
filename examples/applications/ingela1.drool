##needs arrowheads
##needs circle

IngelasArrow: \ p .> [MetaPostArrow p ahLength:2mm]

xmax: \ p .> [point p [continuous_maximizer p (1,0)]].x
xmin: \ p .> [point p [continuous_maximizer p (~1,0)]].x
ymax: \ p .> [point p [continuous_maximizer p (0,1)]].y
ymin: \ p .> [point p [continuous_maximizer p (0,~1)]].y

mediate: \ r x1 x2 .> (1-r)*x1 + r*x2

leftpoint:   \ pic n i .> { bb: [bbox pic]   ([xmin bb], [mediate i/(n+1) [ymin bb] [ymax bb]]) }
rightpoint:  \ pic n i .> { bb: [bbox pic]   ([xmax bb], [mediate i/(n+1) [ymin bb] [ymax bb]]) }
bottompoint: \ pic n i .> { bb: [bbox pic]   ([mediate i/(n+1) [xmin bb] [xmax bb]], [ymin bb]) }
toppoint:    \ pic n i .> { bb: [bbox pic]   ([mediate i/(n+1) [xmin bb] [xmax bb]], [ymax bb]) }

centerat: \ obj z .>
{
  thebb: [bbox obj]
  [[shift (0.5*([xmin thebb]*(z.x-1) - [xmax thebb]*(z.x+1)),
    	   0.5*([ymin thebb]*(z.y-1) - [ymax thebb]*(z.y+1)) )]
   obj]
}
  	   

@width: 0.5bp
& @defaultunit:1%f
|
{

box: \ x0y0 x1y1 .> x0y0--(x0y0.x,x1y1.y)--x1y1--(x1y1.x,x0y0.y)--cycle

stdBox: \ .>
{
  tmp: [box (~5mm,~3mm) (5mm,3mm)]
  ( Hot2D << @nonstroking:GRAY_WHITE|[fill tmp] << [stroke tmp] )
}

rPlus: 2mm
plusCircle: \ .>
{
  c: [circle rPlus]
  res: Hot2D <<
  res << @nonstroking:GRAY_WHITE|[fill c] << [stroke c]
  res << [centerat [TeX `$+$´] (0,0)]
  res;
  res
}

sigmoidPic:
@width:0.3bp &
@stroking:VOID &
@defaultunit:1%f
|
(
  Hot2D << [stroke (~4.5mm,0)--(4.5mm,0)] << [stroke (0,~2.5mm)--(0,2.5mm)] << [stroke (~4.5mm,~2.5mm)>(6mm^0°)--(6mm^180°)<(4.5mm,2.5mm)]
)

templ: \ uLabel symbs:false .>
{
  theColor:[if symbs GRAY_BLACK [gray 0.7]]
  box1: @stroking:theColor|[stdBox]
  sum: [[shift (11.5mm,0)] @stroking:theColor|[plusCircle]]
  box2: [[shift (23mm,0)] box1]
  box3: [[shift (37mm,0)] box1]
  box4: [[shift (0,~7mm)] box1]
  startPoint: \ n pic .> [leftpoint pic 3 n]+(~1cm,(n-2)*3mm)
  lineLeft: \ n pic .> [startPoint n pic]>(^0°)--(^~180°)<[leftpoint pic 3 n]
  res: Hot2D <<
  @stroking:theColor &
  @nonstroking:theColor
  |
  { res   << box1 << sum << box2 << box3 << box4
    	  << [stroke [rightpoint box1 1 1]--[leftpoint sum 1 1] head:IngelasArrow]
    	  << [stroke [rightpoint sum 1 1]--[leftpoint box2 1 1] head:IngelasArrow]
    	  << [stroke [rightpoint box2 1 1]--[leftpoint box3 1 1] head:IngelasArrow]
    	  << [stroke [rightpoint box4 1 1]>(^0°)--(^~90°)<[bottompoint sum 1 1] head:IngelasArrow]
    	  << [stroke [rightpoint box3 1 1]--[leftpoint [[shift (5cm,0)] [plusCircle]] 1 1] head:IngelasArrow]
	  << [stroke [lineLeft 2 box1] head:IngelasArrow]
	  << [[shift [startPoint 2 box1]] [centerat [TeX uLabel] (1,0)]]
  }

   res	  << [if symbs [centerat [TeX `$\beta_{k}$´] (0,0)] NULL]
	  << [if symbs [[shift (0,~7mm)] [centerat [TeX `$\gamma_{k}$´] (0,0)]] NULL]
	  << [if symbs [[shift (23mm,0)] sigmoidPic] NULL]
	  << [if symbs [[shift (37mm,0)] [centerat [TeX `$\alpha_{k}$´] (0,0)]] NULL]
   res;
   res
}

dHat: [normalized (2cm,3cm)]
nDup: 10
dStep: 1mm
d: dStep*dHat*(nDup-1)
dpshift: \ len .> [shift len * dHat]

[[range nDup-'1 '1 '~1].foldl \ p e .> { @<< [[shift (0,2*~15mm)]*[dpshift e*dStep] [templ `$u(t-2)$´]]   p } VOID]
[[range nDup-'1 '1 '~1].foldl \ p e .> { @<< [[shift (0,1*~15mm)]*[dpshift e*dStep] [templ `$u(t-1)$´]]   p } VOID]
[[range nDup-'1 '1 '~1].foldl \ p e .> { @<< [[shift (0,0*~15mm)]*[dpshift e*dStep] [templ `$u(t)$´]]   p } VOID]

@<< [[shift (0,0*~15mm)]*[dpshift 0mm] [templ `$u(t)$´ true]]
@<< [[shift (0,1*~15mm)]*[dpshift 0mm] [templ `$u(t-1)$´ true]]
@<< [[shift (0,2*~15mm)]*[dpshift 0mm] [templ `$u(t-2)$´ true]]


plusBar:
@nonstroking:GRAY_WHITE
& @defaultunit:1%C
|
{
  f: rPlus*[normalized [[rotate 90°] d]]
  a: [angle d]
  cyl: (-f)--(d-f)>(^a)--(^)<(d+rPlus*[normalized d])>(^)--(^a)<(f+d)--f--cycle
  ( Hot2D << [fill cyl] << [stroke cyl] << @nonstroking:GRAY_BLACK|[plusCircle] )
}

lastSum: [[shift (5cm+10mm,~15mm)+dHat*dStep*(nDup/2)] [plusCircle]]
@<< lastSum

muBox: [[shift (50mm,2*~15mm-7mm)] [stdBox]]

@<< [[shift (5cm,0)] plusBar] << [[shift (5cm,~15mm)] plusBar] << [[shift (5cm,2*~15mm)] plusBar]
 << muBox << [[shift (50mm,2*~15mm-7mm)] [centerat [TeX `$\mu$´] (0,0)]]
 << [stroke [rightpoint muBox 1 1]>(^0°)--(^~90°)<[bottompoint lastSum 1 1] head:IngelasArrow]

circleToCircle: \ p1 p2 .>  (p1+rPlus*[normalized p2-p1])--(p2+rPlus*[normalized p1-p2])

@<< [stroke [circleToCircle ((5cm,0*~15mm) + dHat*dStep*(nDup/2))  (5cm+10mm,~15mm)+dHat*dStep*(nDup/2)] head:IngelasArrow]
 << [stroke [circleToCircle ((5cm,1*~15mm) + dHat*dStep*(nDup/2))  (5cm+10mm,~15mm)+dHat*dStep*(nDup/2)] head:IngelasArrow]
 << [stroke [circleToCircle ((5cm,2*~15mm) + dHat*dStep*(nDup/2))  (5cm+10mm,~15mm)+dHat*dStep*(nDup/2)] head:IngelasArrow]

py: [rightpoint lastSum 1 1]
py2: py + (5mm,0)
@<< [stroke py--py2 head:IngelasArrow] << [[shift py2+(1mm,0)] [centerat [TeX `$y(t)$´] (~1,~1)]]

} /* end of "global" dynamic bindings */
