xmax: \ p .> [continuous_maximizer p (1,0)].p.x
xmin: \ p .> [continuous_maximizer p (~1,0)].p.x
ymax: \ p .> [continuous_maximizer p (0,1)].p.y
ymin: \ p .> [continuous_maximizer p (0,~1)].p.y
centerat: \ obj z .>
 {
  thebb: [bbox obj]
  [[shift (0.5*([xmin thebb]*(z.x-1) - [xmax thebb]*(z.x+1)),
    	   0.5*([ymin thebb]*(z.y-1) - [ymax thebb]*(z.y+1)) )]
   obj]
 }

centerxat: \ obj x .>
 {
  thebb: [bbox obj]
  [[shift ( 0.5*([xmin thebb]*(x-1) - [xmax thebb]*(x+1)),
  	    0cm )]
   obj]
 }

centeryat: \ obj y .>
 {
  thebb: [bbox obj]
  [[shift ( 0cm,
    	    0.5*([ymin thebb]*(y-1) - [ymax thebb]*(y+1)) )]
   obj]
 }

centerx: \ obj .> [centerxat obj 0]
centery: \ obj .> [centeryat obj 0]

centeryX:
 {thebb: [bbox [TeX `$X$´]]
  [shift (0cm, ~0.5*([ymax thebb] + [ymin thebb]))]}
centeryatX:
 {thebb: [bbox [TeX `$X$´]]
  \ obj y .> [[shift (0cm, ~(y+1)/2*[ymax thebb])] obj]}

centerof: \ obj .> { thebb: [bbox obj]  (0.5*([xmin thebb]+[xmax thebb]),0.5*([ymin thebb]+[ymax thebb])) }

mediate: \ r x1 x2 .> (1-r)*x1 + r*x2

dynamic @labelmargin identity 1mm

|** The name of the following function is an abbreviation of "centerat, with label margin"
centeratwlm: \ obj z .>
 {
  thebb: [bbox obj]
  [[shift (0.5*(([xmin thebb]-@labelmargin)*(z.x-1) - ([xmax thebb]+@labelmargin)*(z.x+1)),
    	   0.5*(([ymin thebb]-@labelmargin)*(z.y-1) - ([ymax thebb]+@labelmargin)*(z.y+1)) )]
   obj]
 }

|** This function enlarges the bbox so that its center coincides with the origin.
symbboxed: \ pic .>
{
  bb: [bbox pic]
  rx: [max ~[xmin bb] [xmax bb]]
  ry: [max ~[ymin bb] [ymax bb]]
  [bboxed pic [rectangle (~rx,~ry) (rx,ry)]]
}

|** This function enlarges the bbox vertically so that its vertical midpoint is 0.
symbboxedy: \ pic .>
{
  bb: [bbox pic]
  ry: [max ~[ymin bb] [ymax bb]]
  [bboxed pic [rectangle (~[xmin bb],~ry) ([xmax bb],ry)]]
}

|** This function enlarges the bbox horizontally.  Compare symbboxedy.
symbboxedx: \ pic .>
{
  bb: [bbox pic]
  rx: [max ~[xmin bb] [xmax bb]]
  [bboxed pic [rectangle (~rx,[ymin bb]) (rx,[ymax bb])]]
}

|** This function enlarges the bbox in all directions.
enlargebbox: \ pic xy .>
{
  bb: [bbox pic]
  [bboxed pic [rectangle ([xmin bb]-xy.x,[ymin bb]-xy.y) ([xmax bb]+xy.x,[ymax bb]+xy.y)]]
}
