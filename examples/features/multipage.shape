/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

##needs circle

clock: \ hour minute .>
{
  hourLength: 4cm
  minuteLength: 5.5cm

  hourAngle: 90° - (hour/12)*360°
  minuteAngle: 90° - (minute/60)*360°

  [stroke (0cm,0cm)--(hourLength*[dir hourAngle])]
  &
  [stroke (0cm,0cm)--(minuteLength*[dir minuteAngle])]
}

•catalog.[setpagelabel prefix:`Am ´ number:'8]
•catalog.[setbboxgroup 'clock]
•stdout << `First page's label: ´ << •catalog.[nextpagelabel] << `¢n´

•catalog << [clock 8 0]
	    & [destination level:'0 text:`Morning´ color:[rgb 0.5 0.5 0] open:true]
	    & [destination level:'1 text:`Eight´]
	    & {
	      bar: [shift (3cm,1cm)][]stroke[][circle 5mm]
  	      bar
	      &
	      [annotationLink site:[site target:bar
	  			         text:`Leave this place!´
				         width: 2bp]
      	 		   name:`four in the afternoon´]
	    }
         << [clock 9 0]
	    & [destination level:'1 text:`Nine´]

•catalog.[setpagelabel number:•catalog.[nextpagenumber]+'1]
•stdout << `Skipping to page: ´ << •catalog.[nextpagenumber] << `¢n´

•catalog << [clock •catalog.[nextpagenumber]*1 0]

/** Note how the various part of the page label may be set independently of each other.
 **/
•catalog.[setpagelabel prefix:`Pm ´]
•catalog.[setpagelabel number:'33]
•catalog.[setpagelabel style:'alphabet]
•stdout << `Later page's label: ´ << •catalog.[nextpagelabel] << `¢n´

•catalog << [clock 3 10]
  	    & [destination level:'0 text:`Afternoon´ italic:true]
         << [clock 4 10]
  	    & [destination name:`four in the afternoon´]
	 << [clock 5 10]
	    & [destination level:'1 text:`Five´ bold:true]
