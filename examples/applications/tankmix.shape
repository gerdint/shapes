/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

##preamble \RequirePackage{sigsysnotation}

##needs blockdraw_wfo
##needs pipefigprefs

##needs bookprefs

bookprefs & @defaultunit:1%c
|
{ |** book preferences active

tank_W: 3cm
tank_H: 1.5cm
tank_L: tank_H * 0.76

y1: tank_H + 1cm
y2: tank_H + 1mm
r: 5mm
inflow: \ •dst x1 x2 idx .>
{
  d: [normalized x2 - x1]
  •dst << [stroke (x1,y1+@pipe_width/2)--(x2-d*(r+@pipe_width/2),y1+@pipe_width/2)>(^[angle (d,0)])--(^90°)<(x2+d*@pipe_width/2,y1-@pipe_width/2-r)--(x2+d*@pipe_width/2,y2)]
  •dst << [stroke (x1,y1-@pipe_width/2)--(x2-d*(r+@pipe_width/2),y1-@pipe_width/2)>(^[angle (d,0)])--(^90°)<(x2-d*@pipe_width/2,y1-@pipe_width/2-r)--(x2-d*@pipe_width/2,y2)]
  p: (x2-d*(r+7mm),y1)--(x2-d*(r+2mm),y1)
  •dst << [stroke p head:MetaPostArrow]
  •dst << [shift [p 0.5].p+(0mm,@pipe_width/2+2mm)] [] [centerx (TeX [sprintf `$q_{%g}$´ idx])]
  •dst << [shift (x1+d*3mm,y1-@pipe_width/2-2mm)] [] [centerat (TeX `Inflow´) (0,1)]
  v: [shift ([mediate 0.4 x1 x2],y1)]*[rotate 90°] [] [valve]
  •dst << v
  •dst << [shift ([mediate 0.4 x1 x2]+0.5cm,[pathpoint_maximizer [bbox v] (0,1)].p.y)] [] (TeX [sprintf `$u_{%g}$´ idx])
}

[inflow •page ~2.5cm tank_W/3 1]
[inflow •page tank_W+2.5cm tank_W*2/3 2]

•page << @nonstroking:[gray 0.8] | [fill (0cm,0cm)--(tank_W,0cm)--(tank_W,tank_L)--(0cm,tank_L)--cycle]

•page << [stroke (0cm,tank_H)--(0cm,0cm)--(tank_W,0cm)--(tank_W,tank_H)]
{
 p: (tank_W+5mm,0cm)--(tank_W+5mm,tank_L)
 •page << [stroke p head:MetaPostArrow]
 •page << [shift [p 0.5].p+(1mm,0mm)] [] (TeX `$h$´)
}
•page << @nonstroking:GRAY_WHITE | [[shift (tank_W/2, tank_L/2)] [centerlabel (TeX `Tank´)]]
•page << [stroke (tank_W/2,0cm)--(tank_W/2,~1.5cm)]
•page << [stroke (tank_W/2+@pipe_width,0cm)--(tank_W/2+@pipe_width,~1.5cm)]
{
 p: (tank_W/2+@pipe_width/2,~3mm)--(tank_W/2+@pipe_width/2,~13mm)
 •page << [stroke p head:MetaPostArrow]
 •page << [shift [p 0.5].p + (@pipe_width/2+1mm,0mm)] [] (TeX `$q$´)
}

} |** end of book preferences
