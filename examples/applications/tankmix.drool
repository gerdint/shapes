#preamble \RequirePackage{sigsysnotation}

##needs blockdraw_wfo
##needs pipefigprefs

##needs bookprefs

bookprefs & @defaultunit:1%c
|
{ |** book preferences active

TANK_W: 3cm
TANK_H: 1.5cm
TANK_L: TANK_H * 0.76

y1: TANK_H + 1cm
y2: TANK_H + 1mm
R: 5mm
inflow: \ x1 x2 idx .>
{
  d: [normalized x2 - x1]
  @<< [stroke (x1,y1+@PIPE_WIDTH/2)--(x2-d*(R+@PIPE_WIDTH/2),y1+@PIPE_WIDTH/2)>(^[angle (d,0)])--(^90°)<(x2+d*@PIPE_WIDTH/2,y1-@PIPE_WIDTH/2-R)--(x2+d*@PIPE_WIDTH/2,y2)]
  @<< [stroke (x1,y1-@PIPE_WIDTH/2)--(x2-d*(R+@PIPE_WIDTH/2),y1-@PIPE_WIDTH/2)>(^[angle (d,0)])--(^90°)<(x2-d*@PIPE_WIDTH/2,y1-@PIPE_WIDTH/2-R)--(x2-d*@PIPE_WIDTH/2,y2)]
  p: (x2-d*(R+7mm),y1)--(x2-d*(R+2mm),y1)
  @<< [stroke p head:MetaPostArrow]
  @<< [shift [point p 0.5]+(0mm,@PIPE_WIDTH/2+2mm)] [] [centerx [TeX [sprintf `$q_{%g}$´ idx]]]
  @<< [shift (x1+d*3mm,y1-@PIPE_WIDTH/2-2mm)] [] [centerat [TeX `Inflow´] (0,1)]
  v: [shift ([mediate 0.4 x1 x2],y1)]*[rotate 90°] [] [valve]
  @<< v
  @<< [shift ([mediate 0.4 x1 x2]+0.5cm,[discrete_maximizer [bbox v] (0,1)].y)] [] [TeX [sprintf `$u_{%g}$´ idx]]
}

![inflow ~2.5cm TANK_W/3 1]
![inflow TANK_W+2.5cm TANK_W*2/3 2]

@<< @nonstroking:[gray 0.8] | [fill (0cm,0cm)--(TANK_W,0cm)--(TANK_W,TANK_L)--(0cm,TANK_L)--cycle]

@<< [stroke (0cm,TANK_H)--(0cm,0cm)--(TANK_W,0cm)--(TANK_W,TANK_H)]
{
 p: (TANK_W+5mm,0cm)--(TANK_W+5mm,TANK_L)
 @<< [stroke p head:MetaPostArrow]
 @<< [shift [point p 0.5]+(1mm,0mm)] [] [TeX `$h$´]
}
@<< @nonstroking:GRAY_WHITE | [[shift (TANK_W/2, TANK_L/2)] [centerlabel [TeX `Tank´]]]
@<< [stroke (TANK_W/2,0cm)--(TANK_W/2,~1.5cm)]
@<< [stroke (TANK_W/2+@PIPE_WIDTH,0cm)--(TANK_W/2+@PIPE_WIDTH,~1.5cm)]
{
 p: (TANK_W/2+@PIPE_WIDTH/2,~3mm)--(TANK_W/2+@PIPE_WIDTH/2,~13mm)
 @<< [stroke p head:MetaPostArrow]
 @<< [shift [point p 0.5] + (@PIPE_WIDTH/2+1mm,0mm)] [] [TeX `$q$´]
}

} |** end of book preferences
