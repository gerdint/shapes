/** This file is part of Shapes.
 **
 ** Shapes is free software: you can redistribute it and/or modify
 ** it under the terms of the GNU General Public License as published by
 ** the Free Software Foundation, either version 3 of the License, or
 ** any later version.
 **
 ** Shapes is distributed in the hope that it will be useful,
 ** but WITHOUT ANY WARRANTY; without even the implied warranty of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 ** GNU General Public License for more details.
 **
 ** You should have received a copy of the GNU General Public License
 ** along with Shapes.  If not, see <http://www.gnu.org/licenses/>.
 **
 ** Copyright 2008 Henrik Tidefelt
 **/

|** preamble \RequirePackage{../sigsysnotation}

##needs valve
##needs demoprefs
##needs blockdraw

demoprefs & @defaultunit:1%c
|
{

longblockrx: 1cm
longblockry: 5mm

tank_W: 3cm
tank_H: 1.5cm
tank_L: tank_H * 0.76

y1: tank_H + 1cm
y2: tank_H + 1mm
r: 5mm

inflow: \ •dst x1 x2 txt doValve →
{
  d: [normalized x2 - x1]
  •dst << [stroke (x1,y1+@pipe_width/2)--(x2-d*(r+@pipe_width/2),y1+@pipe_width/2)>(^[angle (d,0)])--(^90°)<(x2+d*@pipe_width/2,y1-@pipe_width/2-r)--(x2+d*@pipe_width/2,y2)]
  •dst << [stroke (x1,y1-@pipe_width/2)--(x2-d*(r+@pipe_width/2),y1-@pipe_width/2)>(^[angle (d,0)])--(^90°)<(x2-d*@pipe_width/2,y1-@pipe_width/2-r)--(x2-d*@pipe_width/2,y2)]
  p: (x2-d*(r+7mm),y1)--(x2-d*(r+2mm),y1)
  •dst <<[stroke p head:MetaPostArrow]
  •dst << [[shift (x1+d*3mm,y1-@pipe_width/2-3mm)] [center (TeX txt) (0,1)]]
  •dst << [if doValve
        [[shift ([mediate 0.4 x1 x2],y1)]*[rotate 90°] [valve]]
	null]
}

[inflow •page ~2.5cm tank_W/3 `Acid process flow´ false]
[inflow •page tank_W+2.5cm tank_W*2/3 `NaOH solution´ true]

•page << @nonstroking: [gray 0.8] |
 [fill (0cm,0cm)--(tank_W,0cm)--(tank_W,tank_L)--(0cm,tank_L)--cycle]
{
    hdl: (3mm^45°)
    lambda: (tank_W) / 4
    •page << @width: 0.5bp & @dash: [dashpattern 3mm 1mm] | [stroke (0,tank_L)>hdl--(^)<(lambda,tank_L)>hdl--(^)<(2*lambda,tank_L)>hdl--(^)<(3*lambda,tank_L)>hdl--(^)<(4*lambda,tank_L)>hdl]
}

{
 r: 6mm
 xc: tank_W*0.5
 yc: 5mm
 a: 60°
 •page << [stroke (xc,y1+5mm)--(xc,yc)>(^a)--(^180°-a)<(xc+r,yc)>(^180°+a)--(^~a)<(xc,yc)>(^180°-a)--(^a)<(xc-r,yc)>(^~a)--(^180°+a)<(xc,yc)]
}

•page << [stroke (0cm,tank_H)--(0cm,0cm)--(tank_W,0cm)--(tank_W,tank_H)]
•page << [stroke (tank_W/2,0cm)--(tank_W/2,~1.5cm)]
•page << [stroke (tank_W/2+@pipe_width,0cm)--(tank_W/2+@pipe_width,~1.5cm)]
{
 p: (tank_W/2+@pipe_width/2,~5mm)--(tank_W/2+@pipe_width/2,~10mm)
 •page << [stroke p head:MetaPostArrow]
 •page << [putlabelRight (TeX `Flow with desired pH´) [p 0.5].p+(@pipe_width/2,0) 0]
}

}
